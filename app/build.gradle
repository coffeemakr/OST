plugins {
    id "org.sonarqube" version "2.5"
    id "net.ltgt.errorprone" version "0.0.11"
}


apply plugin: 'com.android.application'
apply plugin: 'jacoco-android'

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-XepDisableWarningsInGeneratedCode']
}

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    signingConfigs {
        travisSigningConfig {
            keyAlias "Travis OST"
            keyPassword System.getenv("KEY_PASSWORD")
            storeFile file('../travis-release-key.jks')
            storePassword System.getenv("KEYSTORE_PASSWORD")
        }
    }
    defaultConfig {
        applicationId "ch.unstable.ost"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 7
        versionName "0.4.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
        signingConfig file('../travis-release-key.jks').exists() ? signingConfigs.travisSigningConfig : null

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
        execution 'ANDROID_TEST_ORCHESTRATOR'
        unitTests.all {
            testLogging {
                events 'skipped', 'failed', 'standardOut', 'standardError'
                exceptionFormat "short"
            }
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
    buildTypes {
        release {
            shrinkResources false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    productFlavors {
    }
}

jacocoAndroidUnitTestReport {
    csv.enabled false
    html.enabled true
    xml.enabled true
}


dependencies {
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile('com.google.guava:guava:23.0-android', {
        exclude group: 'com.google.code.findbugs'
    })
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.mikael:urlbuilder:2.0.9'
    compile 'commons-io:commons-io:2.5'
    compile 'cat.ereza:customactivityoncrash:2.1.0'
    compile 'com.google.code.gson:gson:2.8.1'
    compile "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile "com.android.support:design:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:support-v4:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:cardview-v7:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:preference-v7:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:support-vector-drawable:${rootProject.ext.supportLibVersion}"
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.10.0'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'

    // Room
    implementation "android.arch.persistence.room:runtime:${rootProject.ext.roomLibVersion}"
    annotationProcessor "android.arch.persistence.room:compiler:${rootProject.ext.roomLibVersion}"
    androidTestImplementation "android.arch.persistence.room:testing:${rootProject.ext.roomLibVersion}"
    implementation "android.arch.persistence.room:rxjava2:${rootProject.ext.roomLibVersion}"

    // Paging
    compile 'android.arch.paging:runtime:1.0.0-alpha2'
    errorprone 'com.google.errorprone:error_prone_core:2.1.1'

    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestUtil 'com.android.support.test:orchestrator:1.0.1'
}
