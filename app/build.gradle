plugins {
    id "org.sonarqube" version "2.5"
    id "net.ltgt.errorprone" version "0.0.11"
}


apply plugin: 'com.android.application'
apply plugin: 'jacoco-android'

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-XepDisableWarningsInGeneratedCode']
}

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    signingConfigs {
        travisSigningConfig {
            keyAlias "Travis OST"
            keyPassword System.getenv("KEY_PASSWORD")
            storeFile file('../travis-release-key.jks')
            storePassword System.getenv("KEYSTORE_PASSWORD")
        }
        privateSigningConfig {
            keyAlias 'key0'
            keyPassword 'ost private keystore'
            storeFile file('/home/coffeemakr/ost_private.jks')
            storePassword 'ost private keystore'
        }
    }


    defaultConfig {
        applicationId "ch.unstable.ost"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 7
        versionName "0.4.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
        signingConfig file('../travis-release-key.jks').exists() ? signingConfigs.travisSigningConfig : null

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events 'skipped', 'failed', 'standardOut', 'standardError'
                exceptionFormat "short"
            }
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    buildTypes {
        release {
            shrinkResources false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

jacocoAndroidUnitTestReport {
    csv.enabled false
    html.enabled true
    xml.enabled true
}


dependencies {
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile('com.google.guava:guava:23.0-android', {
        exclude group: 'com.google.code.findbugs'
    })
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.mikael:urlbuilder:2.0.9'
    compile 'commons-io:commons-io:2.5'
    compile 'cat.ereza:customactivityoncrash:2.1.0'
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    compile 'com.android.support:preference-v7:26.1.0'
    compile 'com.android.support:support-vector-drawable:26.1.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.10.0'

    // Room
    implementation "android.arch.persistence.room:runtime:1.0.0-beta1"
    annotationProcessor 'android.arch.persistence.room:compiler:1.0.0-beta1'
    testImplementation "android.arch.persistence.room:testing:1.0.0-beta1"
    implementation "android.arch.persistence.room:rxjava2:1.0.0-beta1"

    // Paging
    implementation "android.arch.paging:runtime:1.0.0-alpha2"

    errorprone 'com.google.errorprone:error_prone_core:2.1.1'
}
