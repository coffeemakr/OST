apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    //buildToolsVersion rootProject.ext.buildToolsVersion

    signingConfigs {
        travisSigningConfig {
            keyAlias "Travis OST"
            keyPassword System.getenv("KEY_PASSWORD")
            storeFile file('../travis-release-key.jks')
            storePassword System.getenv("KEYSTORE_PASSWORD")
        }
    }
    defaultConfig {
        applicationId "ch.unstable.ost"
        minSdkVersion 21
        targetSdkVersion 29
        multiDexEnabled true
        versionCode 10
        versionName "0.6.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        vectorDrawables.useSupportLibrary = true
        signingConfig file('../travis-release-key.jks').exists() ? signingConfigs.travisSigningConfig : null

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        unitTests.all {
            testLogging {
                events 'skipped', 'failed', 'standardOut', 'standardError'
                exceptionFormat "short"
            }
        }
    }
    buildTypes {
        release {
            shrinkResources false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
            //minifyEnabled true
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    productFlavors {
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation project(':lib:common')
    implementation project(':lib:sbb')

    androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0')
    implementation('com.google.guava:guava:23.0-android', {
        exclude group: 'com.google.code.findbugs'
    })
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    implementation 'commons-io:commons-io:2.6'
    implementation 'cat.ereza:customactivityoncrash:2.1.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.9'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.19.0'

    // Room
    implementation 'androidx.room:room-runtime:2.2.5'
    kapt 'androidx.room:room-compiler:2.2.5'
    androidTestImplementation 'androidx.room:room-testing:2.2.5'
    implementation 'androidx.room:room-rxjava2:2.2.5'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestUtil 'androidx.test:orchestrator:1.2.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.multidex:multidex:2.0.1'

}
repositories {
    mavenCentral()
}
